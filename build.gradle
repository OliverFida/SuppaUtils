plugins {
    id 'fabric-loom' version "${loom_version}"
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    maven {
        url = "https://maven.terraformersmc.com/"
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        "suppautils" {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

}

dependencies {
    // To change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_version}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Mod menu
    modImplementation("com.terraformersmc:modmenu:${modmenu_version}") {
        exclude group: 'net.fabricmc.fabric-api'
    }
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

jar {
    def jarName = "${project.archives_base_name}-${project.mod_version}"
    if (project.hasProperty('mod_beta') && project.mod_beta.toBoolean()) {
        jarName += "-beta"
    }
    if (project.hasProperty('mod_dev_version') && project.mod_dev_version.toBoolean()) {
        jarName += "-dev"
    }
    archiveFileName = "${jarName}-mc${project.minecraft_version}.jar"

    inputs.property "archivesName", project.base.archivesName
    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

tasks.named("remapJar") {
    archiveFileName = jar.archiveFileName
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}